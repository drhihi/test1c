
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	Если ВыполнитьСинхронизациюНаСервере() Тогда
		Сообщить("Синхронизация прошла успешно!");
	Иначе
		Сообщить("При синхронизации были ошибки!");
	КонецЕсли
КонецПроцедуры

&НаСервере
Функция ВыполнитьСинхронизациюНаСервере()
	
	Попытка
		ВСОпределение = Новый WSОпределения("http://192.168.1.190/base/ws/ОбменДанными.1cws?wsdl");
		//ВСОпределение = Новый WSОпределения("http://192.168.1.190/base/ws/ОбменДанными.1cws?wsdl");
		ВСервер = ВСОпределение.Сервисы.Получить("DataTransfer", "ОбменДанными");
		ВТочкаВхода = ВСервер.ТочкиПодключения.Получить("ОбменДаннымиSoap");
		ВОперация = ВТочкаВхода.Интерфейс.Операции.Получить("Синхронизация");
		
		Данные = Новый ХранилищеЗначения(СформироватьСообщениеСерверу(), Новый СжатиеДанных(9));
		
		ДанныеXDTO = ВСОпределение.ФабрикаXDTO.Создать(ВОперация.Параметры.Получить("Данные").Тип, Данные);
		
		ВСПрокси = Новый WSПрокси(ВСОпределение, "DataTransfer", "ОбменДанными", "ОбменДаннымиSoap");
		Ответ = ВСПрокси.Синхронизация(ДанныеXDTO);
		ПринятьИзменениеПоПлату(Ответ.Получить());
		
		Возврат Истина;
		
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат Ложь;
		
	КонецПопытки;
	
КонецФункции

&НаСервере
Процедура ПринятьИзменениеПоПлату(СтрокаСообщения)

	ЧтениеХМЛ = Новый ЧтениеXML;
	ЧтениеХМЛ.УстановитьСтроку(СтрокаСообщения);
	
	ЧтениеСообщения = ПланыОбмена.СоздатьЧтениеСообщения();
	ЧтениеСообщения.НачатьЧтение(ЧтениеХМЛ);
	
	Пока ВозможностьЧтенияXML(ЧтениеХМЛ) Цикл
		Данные = ПрочитатьXML(ЧтениеСообщения.ЧтениеXML);
		Если Не Данные = Неопределено Тогда
			
			Данные.ОбменДанными.Отправитель = ЧтениеСообщения.Отправитель;
			Данные.ОбменДанными.Загрузка = Истина;
			
			Данные.Записать();
			
		КонецЕсли;
	КонецЦикла;
	
	ЧтениеСообщения.ЗакончитьЧтение();
	
КонецПроцедуры

&НаСервере
Функция СформироватьСообщениеСерверу()
	
	ЗаписьХМЛ = Новый ЗаписьXML;
	ЗаписьХМЛ.УстановитьСтроку();
	ЗаписьСообщения = ПланыОбмена.СоздатьЗаписьСообщения();
	Узлы = ПланыОбмена.ОбменСМобильнымиУстройствами.Выбрать();
	Пока Узлы.Следующий() Цикл
		Если Узлы.Ссылка <> ПланыОбмена.ОбменСМобильнымиУстройствами.ЭтотУзел() Тогда
			Узел = Узлы.Ссылка;
		КонецЕсли;
	КонецЦикла;
	ЗаписьСообщения.НачатьЗапись(ЗаписьХМЛ, Узел);
	ВыборкаИзменений = ПланыОбмена.ВыбратьИзменения(Узел, ЗаписьСообщения.НомерСообщения);
	Пока ВыборкаИзменений.Следующий() Цикл
		ОбъектОбмена = ВыборкаИзменений.Получить();
		ЗаписатьXML(ЗаписьХМЛ, ОбъектОбмена);
	КонецЦикла;
	ЗаписьСообщения.ЗакончитьЗапись();
	
	Возврат ЗаписьХМЛ.Закрыть();
	
КонецФункции